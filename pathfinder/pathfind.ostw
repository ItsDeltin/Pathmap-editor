struct PfMap {
    # The points in the map.
    public Vector[] nodes;
    # `[node][i][0]` = neighbor index,
    # `[node][i][1][j]` = attributes
    public Any[][][] neighbors;
}

struct PfNode {
    public Number gScore;
    public Number fScore;
    public Number parent;
    public Number nodeIndex;
}

# The currently loaded map
globalvar PfMap pfLoadedMap;
globalvar Number[][] pfTableTemplate;
globalvar Number pfIterationsThisTick;

# The final pathfinding target.
playervar Vector pfDestination;
# The attributes assigned to the player.
playervar Number[] pfPlayerAttributes;
# The attributes of a player's current pathfinding edge.
playervar Number[] pfAttributes;

# The saved target. If the distance between this and pfDestination becomes
# too large, the pathfinding path will be updated.
playervar Vector pfSavedTarget;
# Determines if the player is pathfinding.
playervar Boolean pfIsPathfinding;
# The current node that the player is walking to.
# If this is -1 but `pfIsPathfinding` is true, then the player
# is walking to `pfDestination`.
playervar Number pfCurrentNodeIndex;
# The position that the player is actively walking towards.
playervar Vector pfWalkingToPosition;
# The nextNode array for pathfinding.
playervar Number[] pfPath;
playervar Number pfStartTime;
playervar Number pfDistanceToNextNode;
playervar Vector pfNextPosition;
# The node closest to the player.
playervar Number pfActiveNode;

rule: '【PF Core】Init loaded map'
if (pfLoadedMap.nodes)
{
    pfTableTemplate = [pfLoadedMap.nodes.Map(n => -1), pfLoadedMap.nodes.Map(n => 9999)];
}

rule: '【PF Core】Next Tick'
if (pfIterationsThisTick <= 0)
{
    pfIterationsThisTick = Max(Min(RoundToInteger(63 - ServerLoad() / 4, Rounding.Nearest), 15), 5);
    Wait(ServerLoadAverage() < 250 ? 0.016 : 0.2);
    LoopIfConditionIsTrue();
}

void pfTick() '【PF Core Subroutine】☞ Tick'
{        
    while (pfIterationsThisTick <= 0) {
        WaitUntil(pfIterationsThisTick > 0, 9999);
    }
    pfIterationsThisTick--;
}

rule: '【PF Core】Update & Execute'
Event.OngoingPlayer
if (pfDestination)
if (EventPlayer().DistanceTo(pfDestination) > 4)
if (!pfSavedTarget || pfDestination.DistanceTo(pfSavedTarget) > 5)
{
    # Debug Message
    SmallMessage(AllPlayers(), <'Updated pathfinder for <0>', EventPlayer()>);
    # Update saved destination
    pfSavedTarget = pfDestination;
    aStar();
    pfIsPathfinding = true;
    pfCurrentNodeIndex = pfPath.First;
    pfWalkingToPosition = pfLoadedMap.nodes[pfCurrentNodeIndex];
    UpdateNodeMeta();
    StartThrottleInDirection(EventPlayer(), PositionOf().DirectionTowards(pfWalkingToPosition), 1, Relative.ToWorld, ThrottleBehavior.ReplaceExistingThrottle, ThrottleRev.DirectionAndMagnitude);
    Wait(2);
    LoopIfConditionIsTrue();
}

rule: '【PF Core】Node reached'
Event.OngoingPlayer
if (pfIsPathfinding)
if (EventPlayer().DistanceTo(pfWalkingToPosition) <= 0.4) {
    # The player reached the next node in the path to the destination.
    if (pfCurrentNodeIndex == -1) {
        # The destination was reached, stop pathfinding.
        StopThrottleInDirection(EventPlayer());
        pfIsPathfinding = false;
    } else {
        ProgressEventPlayer();
    }
    Wait(0.2);
    LoopIfConditionIsTrue();
}

disabled rule: '【PF Core】(optional) Skip node when shortcut is found'
Event.OngoingPlayer
if (pfIsPathfinding)
if (pfCurrentNodeIndex != -1)
if (IsInLineOfSight(PositionOf() + Up() * 0.25, pfNextPosition + Up() * 0.25))
if ((PositionOf().Y - pfNextPosition.Y).Abs() < 1)
{
    // SmallMessage(AllPlayers(), 'Compatible node skip found');
    Wait(0.1);
    ProgressEventPlayer();
    Wait(0.1);
    LoopIfConditionIsTrue();
}

disabled rule: '【PF Core】(optional) Teleport player to next node when player gets stuck.'
Event.OngoingPlayer
if (pfIsPathfinding)
if (pfDistanceToNextNode - 5.5 * 0.5 * (TotalTimeElapsed() - pfStartTime) < 0)
{
    // SmallMessage(AllPlayers(), <'Player <0> got stuck, teleporting them.', EventPlayer()>);
    Teleport(EventPlayer(), pfWalkingToPosition);
    Wait(3);
    LoopIfConditionIsTrue();
}

rule: '【PF Core】(optional) Error when pathfinding without map'
Event.OngoingPlayer
if (!pfLoadedMap.nodes)
if (pfDestination)
{
    LogToInspector(<'Error: Attempted to pathfind player <0> without a loaded map.', EventPlayer()>);
}

void ProgressEventPlayer() playervar '【PF Core Subroutine】☞ Progress event player' {
    # Update the current pathfind attributes.
    # Any rules watching these with the matching condition will activate.
    pfAttributes = pfLoadedMap.neighbors[pfCurrentNodeIndex].FilteredArray(segment => segment[0] == pfPath.First).First.Last;
    # Go to the next node in the path.
    pfCurrentNodeIndex = pfPath.First;
    # Pop the current path.
    pfPath.ModRemoveByIndex(0);
    # Set the position that the player is currently walking towards.
    pfWalkingToPosition = pfCurrentNodeIndex == -1 ? pfDestination : pfLoadedMap.nodes[pfCurrentNodeIndex];
    UpdateNodeMeta();
}

void UpdateNodeMeta() playervar '【PF Core Subroutine】☞ Update node meta' {
    pfActiveNode = pfCurrentNodeIndex;
    # Update the pathfinding start time.
    pfStartTime = TotalTimeElapsed();
    pfDistanceToNextNode = EventPlayer().DistanceTo(pfWalkingToPosition);
    // note: this doesn't seem right?
    pfNextPosition = pfPath.Length >= 2 ? pfDestination : pfLoadedMap.nodes[pfPath[1]];
}

# Selects a node index from a position.
Number chooseNodeFromPosition(in Vector vector) {
    return pfLoadedMap.nodes.IndexOf(pfLoadedMap.nodes
        .SortedArray(n => n.DistanceTo(vector))
        .Slice(0, 5)
        .SortedArray(n => !(IsInLineOfSight(n + Up() * 2, vector + Up() * 2) && (vector.Y - n.Y).Abs() < vector.DistanceTo(n) / 2))
        .First);
}

# Gets the shortest path from start to goal.
void aStar() {
    Number goal = chooseNodeFromPosition(pfDestination);
    pfTick();
    if (pfIsPathfinding && pfCurrentNodeIndex != -1) {
        pfActiveNode = pfCurrentNodeIndex;
    } else {
        pfActiveNode = chooseNodeFromPosition(PositionOf());
        pfTick();
    }

    Number heuristicWeight: 20;
    const Number => Number h: n => (pfLoadedMap.nodes[n]).DistanceTo(pfLoadedMap.nodes[pfActiveNode]) * heuristicWeight;

    PfNode[] scores = [{ parent: -1, gScore: 0, fScore: 0, nodeIndex: goal }];
    Number[] open = [0];

    Number iter = 0;
    while (open.Length) {
        // Debug
        // Any[] icons = [];
        // foreach (Number o in open) {
        //     icons += CreateInWorldText(AllPlayers(), scores[o].nodeIndex, pfLoadedMap.nodes[scores[o].nodeIndex], 2, Clipping.DoNotClip, InworldTextRev.VisibleTo, Color.White);
        // }
        // WaitForClick();
        // foreach (Any icon in icons) {
        //     DestroyInWorldText(icon);
        // }

        Number current = open.SortedArray((o, i) => scores[o].fScore).First;
        Number currentNodeIndex: scores[current].nodeIndex;
        # If the goal was found, reconstruct the path.
        if (currentNodeIndex == pfActiveNode) {
            // pfIsPartialPath = iter == 20;
            reconstructPath(scores, current);
            return;
        }

        // Any[] neighborIcons = [CreateInWorldText(AllPlayers(), currentNodeIndex, pfLoadedMap.nodes[currentNodeIndex], 2, Clipping.DoNotClip, InworldTextRev.VisibleTo, Color.Rose)];
        open.ModRemoveByValue(current);

        # Check each neighbor to the current node.
        foreach (Any[] neighbor in pfLoadedMap.neighbors[currentNodeIndex]) {
            # The neighbor index to be used like `pfLoadedMap.nodes[neighborI]`.
            Number neighborI = neighbor[0];
            Number scoreIndex = scores.Map(s => s.nodeIndex).IndexOf(neighborI);

            // Color neighborColor = Color.Red;
            # Ensure that the path to the neighbor is enabled.
            if ((<Number[]>neighbor[1]).IsTrueForAll(a => pfPlayerAttributes.Contains(a))) {
                // neighborColor = Color.Green;
                Number score = scores[current].gScore + pfLoadedMap.nodes[currentNodeIndex].DistanceTo(pfLoadedMap.nodes[neighborI]);

                # Get neighbor index in scores.
                # If -1, the neighbor was not added to the score list yet.
                if (scoreIndex == -1) {
                    # Note: not required after debugging.
                    // scoreIndex = scores.Length;
                    open += scores.Length;
                    scores += { parent: current, gScore: score, fScore: score + h(neighborI), nodeIndex: neighborI };
                } else if (score < scores[scoreIndex].gScore) {
                    scores[scoreIndex] = { parent: current, gScore: score, fScore: score + h(neighborI), nodeIndex: neighborI };
                    if (!open.Contains(scoreIndex))
                        open += scoreIndex;
                }
                // else {
                //     neighborColor = Color.Blue;
                // }

            }
            pfTick();
            // neighborIcons += CreateInWorldText(AllPlayers(), scores[scoreIndex].nodeIndex, pfLoadedMap.nodes[scores[scoreIndex].nodeIndex], 2, Clipping.DoNotClip, InworldTextRev.VisibleTo, neighborColor);
        }

        // WaitForClick();
        // foreach (Any icon in neighborIcons) {
        //     DestroyInWorldText(icon);
        // }
        iter++;
    }
}

void reconstructPath(in PfNode[] scores, ref Number current) {
    pfPath = [];
    while (current != -1) {
        pfPath += scores[current].nodeIndex;
        current = scores[current].parent;
        pfTick();

        // if (current != -1)
        //     CreateBeamEffect(AllPlayers(), BeamType.GoodBeam, pfLoadedMap.nodes[pathArray.Last], pfLoadedMap.nodes[current], Color.Rose, EffectRev.VisibleTo);
    }
    pfPath += [-1];

    // WaitForClick();
    // DestroyAllEffects();
}

void WaitForClick()
{
    WaitUntil(!IsButtonHeld(HostPlayer(), Button.PrimaryFire), 0.2);
    WaitUntil(IsButtonHeld(HostPlayer(), Button.PrimaryFire), 9999);
}