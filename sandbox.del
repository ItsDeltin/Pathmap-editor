import 'main.del';
import 'cursor menu adapter.del';

playervar Number freecamSwitchID!;
playervar Number linkModeID!;
playervar Number attributeDecrementID!;
playervar Number attributeTextID!;
playervar Number attributeIncrementID!;

enum MenuAction
{
    Back = 1,
    FreecamIncrement = 2,
    FreecamDecrement = 3,
    Undo = 4,
    Redo = 5,
    LinkMode = 6,
    AttributeIncrement = 7,
    AttributeIncrementBig = 8,
    AttributeDecrement = 9,
    AttributeDecrementBig = 10
}

rule: 'CM: Make buttons: Main'
Event.OngoingPlayer
{
    createButton(
        '← undo',
        primaryAction: MenuAction.Undo,
        scale: 2,
        posX: -2.1,
        posY: -1,
        clickWidth: 0.4,
        clickHeight: 0.3,
        defaultColor: Color.Yellow,
        hoverColor: Color.Orange);
    createButton(
        'redo →',
        primaryAction: MenuAction.Redo,
        scale: 2,
        posX: -1.6,
        posY: -1,
        clickWidth: 0.4,
        clickHeight: 0.3,
        defaultColor: Color.Aqua,
        hoverColor: Color.SkyBlue);
    createButton(
        'main',
        scale: 2,
        posX: 0,
        posY: 1,
        defaultColor: Color.Yellow,
        interactable: false);
    createButton(
        'back',
        primaryAction: MenuAction.Back,
        scale: 2,
        posX: -2,
        posY: 1,
        defaultColor: Color.Red);
    
    freecamSwitchID = createButton(
        'freecam: off',
        primaryAction: MenuAction.FreecamIncrement,
        secondaryAction: MenuAction.FreecamDecrement,
        scale: 2.5,
        posX: -1.7,
        posY: 0.6,
        defaultColor: Color.White);
    
    linkModeID = createButton(
        'Node link mode (segment)',
        primaryAction: MenuAction.LinkMode,
        scale: 2.5,
        posY: 0.7);
    
    attributeIncrementID = createButton(
        '→',
        primaryAction: MenuAction.AttributeIncrement,
        secondaryAction: MenuAction.AttributeIncrementBig,
        scale: 4,
        posY: 0.5,
        posX: 0.1775,
        visible: false);
    attributeDecrementID = createButton(
        '←',
        primaryAction: MenuAction.AttributeDecrement,
        secondaryAction: MenuAction.AttributeDecrementBig,
        scale: 4,
        posY: 0.5,
        posX: -0.1775,
        visible: false,
        useDefaultFont: false);
    attributeTextID = createButton(
        '0',
        scale: 2.5,
        posY: 0.5,
        defaultColor: Color.Yellow,
        interactable: false, 
        visible: false);
}

rule: 'CM: Freecam increment'
Event.OngoingPlayer
if (cm_currActionID == MenuAction.FreecamIncrement)
{
    cameraMode++;
    if (cameraMode == 3) cameraMode = 0;
    updateFreecamLabel();
}

rule: 'CM: Freecam decrement'
Event.OngoingPlayer
if (cm_currActionID == MenuAction.FreecamDecrement)
{
    cameraMode--;
    if (cameraMode == -1) cameraMode = 2;
    updateFreecamLabel();
}

rule: 'CM: Undo'
Event.OngoingPlayer
if (cm_currActionID == MenuAction.Undo)
if (CanUndo)
{
    Undo();
}

rule: 'CM: Redo'
Event.OngoingPlayer
if (cm_currActionID == MenuAction.Redo)
if (CanRedo)
{
    Redo();
}

rule: 'CM: Change link mode'
Event.OngoingPlayer
if (cm_currActionID == MenuAction.LinkMode)
{
    attributeMode = !attributeMode;
    SetLabel(linkModeID, !attributeMode ? 'Node link mode (segment)' : 'Node link mode (attribute)');
    SetVisible(attributeDecrementID, attributeMode);
    SetVisible(attributeTextID, attributeMode);
    SetVisible(attributeIncrementID, attributeMode);
}

rule: 'CM: Attribute change'
Event.OngoingPlayer
if (cm_currActionID == MenuAction.AttributeIncrement ||
    cm_currActionID == MenuAction.AttributeIncrementBig ||
    cm_currActionID == MenuAction.AttributeDecrement ||
    cm_currActionID == MenuAction.AttributeDecrementBig)
{
    if (cm_currActionID == MenuAction.AttributeIncrement)
        attribute++;
    else if (cm_currActionID == MenuAction.AttributeIncrementBig)
        attribute += 5;
    else if (cm_currActionID == MenuAction.AttributeDecrement)
        attribute--;
    else if (cm_currActionID == MenuAction.AttributeDecrementBig)
        attribute -= 5;

    SetLabel(attributeTextID, <String>attribute);
}

void updateFreecamLabel() '(Subroutine) CM: Update freecam label'
{
    SetLabel(freecamSwitchID, ['freecam: off', 'freecam: on', 'freecam: gimbal'][cameraMode]);
}